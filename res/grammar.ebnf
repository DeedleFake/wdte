    <script> -> <cexprs> Ω
  <funcmods> -> <funcmod> <funcmods>
              | ε
   <funcmod> -> memo
  <patdecls> -> <patdecl> <patdecls>
              | ε
   <patdecl> -> id
              | [ <argdecls> ; ]
  <argdecls> -> id <argdecls>
              | ε
      <expr> -> <single> <args> <switch> <slot> <chain>
      <args> -> <single> <args>
              | ε
     <chain> -> -> <expr>
              | -- <expr>
              | -| <expr>
              | ε
      <slot> -> : <patdecl>
              | ε
    <single> -> number
              | string
              | <import>
              | <lambda>
              | <array>
              | <subbable>
  <subbable> -> id <sub>
              | <compound> <sub>
       <sub> -> . <subbable>
              | ε
     <array> -> [ <aexprs> ]
    <aexprs> -> <exprs>
              | ;
    <switch> -> { <switches> }
              | ε
  <compound> -> ( <cexprs> )
              | (| <cexprs> |)
     <exprs> -> <expr> ; <exprs>
              | ε
  <switches> -> <expr> => <expr> ; <switches>
              | ε
    <cexprs> -> <expr> ; <cexprs>
              | <letexpr> ; <cexprs>
              | ε
    <lambda> -> (@ <funcmods> id <patdecls> => <cexprs> )
   <letexpr> -> let <letassign>
 <letassign> -> <funcmods> id <patdecls> => <expr>
              | <patdecl> => <expr>
    <import> -> import string

# vim: ts=2 sw=2 et
