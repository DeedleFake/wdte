   <script> -> <imports> <funcdecls>
  <imports> -> <import> <imports>
             | ε
   <import> -> string -> id ;
<funcdecls> -> <funcdecl> <funcdecls>
             | ε
 <funcdecl> -> id <argdecls> = <expr> ;
 <argdecls> -> id <argdecls>
             | ε
     <expr> -> <func> <args>
             | { <exprs> }
             | switch <expr> { <switches> }
     <args> -> <arg> <args>
             | ε
      <arg> -> <single>
             | ( <expr> )
    <exprs> -> <expr> ; <exprs>
             | ( <expr> ) <exprs>
             | ε
 <switches> -> <expr> = <expr> ; <switches>
             | ε
   <single> -> <func>
             | number
             | string
             | true
             | false
             | nil
             | <array>
    <array> -> [ <exprs> ]
     <func> -> id <subfunc>
  <subfunc> -> . id
             | ε

# vim: et
