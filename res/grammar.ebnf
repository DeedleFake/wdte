   <script> -> <decls> Ω
    <decls> -> <decl> <decls>
             | ε
     <decl> -> <import>
             | <funcdecl>
   <import> -> string => id ;
 <funcdecl> -> id <argdecls> => <expr> ;
 <argdecls> -> id <argdecls>
             | ε
     <expr> -> <single> <args> <chain>
    <chain> -> -> <single> <args> <chain>
             | ε
     <args> -> <single> <args>
             | ε
    <exprs> -> <expr> ; <exprs>
             | ε
 <switches> -> <expr> => <expr> ; <switches>
             | default => <expr> ; <switches>
             | ε
   <single> -> <func>
             | <array>
             | <switch>
             | <compound>
             | number
             | string
    <array> -> [ <exprs> ]
   <switch> -> switch <expr> { <switches> }
 <compound> -> ( <exprs> )
     <func> -> id <subfunc>
  <subfunc> -> . id
             | ε

# vim: ts=2 sw=2 et
