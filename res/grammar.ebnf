  <script> -> <cexprs> Ω
<funcmods> -> <funcmod> <funcmods>
            | ε
 <funcmod> -> memo
<argdecls> -> id <argdecls>
            | ε
    <expr> -> <single> <args> <slot> <chain>
    <args> -> <single> <args>
            | ε
   <chain> -> -> <expr>
            | -- <expr>
            | ε
    <slot> -> : id
            | ε
  <single> -> number
            | string
            | <import>
            | <lambda>
            | <array>
            | <subbable>
<subbable> -> id <sub>
            | <switch> <sub>
            | <compound> <sub>
     <sub> -> . <subbable>
            | ε
   <array> -> [ <exprs> ]
  <switch> -> switch <expr> { <switches> }
<compound> -> ( <cexprs> )
   <exprs> -> <expr> ; <exprs>
            | ε
<switches> -> <expr> => <expr> ; <switches>
            | default => <expr> ; <switches>
            | ε
  <cexprs> -> <expr> ; <cexprs>
            | <letexpr> ; <cexprs>
            | ε
  <lambda> -> (@ <funcmods> id <argdecls> => <expr> ; )
 <letexpr> -> let <funcmods> id <argdecls> => <expr>
   <import> -> import string

# vim: ts=2 sw=2 et
